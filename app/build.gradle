apply plugin: 'com.android.application'

android {
	namespace 'de.markusfisch.android.motoscore'
	compileSdk sdk_version

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion sdk_version

		// Because of the Google libraries.
		multiDexEnabled true

		versionCode 23
		versionName '1.6.8'

		vectorDrawables.useSupportLibrary true
	}

	signingConfigs {
		release {
			keyAlias System.getenv('ANDROID_KEY_ALIAS')
			keyPassword System.getenv('ANDROID_KEY_PASSWORD')
			storePassword System.getenv('ANDROID_STORE_PASSWORD')
			def filePath = System.getenv('ANDROID_KEYFILE')
			storeFile filePath ? file(filePath) : null
		}
	}

	buildTypes {
		debug {
			applicationIdSuffix '.debug'
		}

		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), \
				'proguard-rules.txt'
			signingConfig signingConfigs.release
		}
	}
}

dependencies {
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'com.google.android.material:material:1.10.0'
	implementation 'com.google.android.gms:play-services-maps:17.0.1'
}
